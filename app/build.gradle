import id.petersam.buildsrc.BuildAndroidConfig
import id.petersam.buildsrc.BuildModules
import id.petersam.buildsrc.dependencies.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion BuildAndroidConfig.COMPILE_SDK_VERSION

    defaultConfig {
        applicationId "id.petersam.base"
        minSdkVersion BuildAndroidConfig.MIN_SDK_VERSION
        targetSdkVersion BuildAndroidConfig.TARGET_SDK_VERSION
        versionCode BuildAndroidConfig.VERSION_CODE
        versionName BuildAndroidConfig.VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        develop {
            dimension "version"
            resValue "string", "app_name", "Base Dev"
            buildConfigField "String", "API_BASE_URL", API_BASE_URL_DEV
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        staging {
            dimension "version"
            resValue "string", "app_name", "Base Staging"
            buildConfigField "String", "API_BASE_URL", API_BASE_URL_STAGING
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        production {
            dimension "version"
            resValue "string", "app_name", "Base"
            buildConfigField "String", "API_BASE_URL", API_BASE_URL
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(BuildModules.CORE)
    implementation project(BuildModules.Features.LOGIN)
    implementation project(BuildModules.Features.HOME)

    implementation Dependencies.CONSTRAINT_LAYOUT
    implementation Dependencies.NAVIGATION_FRAGMENT
    implementation Dependencies.NAVIGATION_UI
    implementation Dependencies.MATERIAL
}
